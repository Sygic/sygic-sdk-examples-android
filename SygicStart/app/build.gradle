plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    compileSdkVersion compile_sdk_version
    buildToolsVersion build_tools_version

    defaultConfig {
        applicationId "com.sygic.sdk.example"
        minSdkVersion 21
        targetSdkVersion target_sdk_version
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // use your own SDK client ID
        buildConfigField "String", "SYGIC_SDK_CLIENT_ID", getSdkClientId()
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation("com.sygic.sdk:maps-android:${sygic_sdk_version}@aar") {
        transitive = true
    }

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.5'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.5'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.appcompat:appcompat:1.4.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
}

def getSdkClientId() {
    def sdkClientId = ""

    def localProperties = project.rootProject.file('local.properties')
    if (localProperties.exists()) {
        Properties properties = new Properties()
        properties.load(localProperties.newDataInputStream())
        sdkClientId = properties.getProperty('sygic.sdk.client.id', "")
    }

    if (sdkClientId.isEmpty()) {
        logger.error('Missing Client ID for Sygic Maps SDK! Define one in local.properties as sygic.sdk.client.id="YOUR.CLIENT.ID"')
    }

    return sdkClientId
}
